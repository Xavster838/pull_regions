from snakemake.utils import min_version
import pandas as pd
import os

min_version("6.0")

# define your configuration via python
# or define a yaml e.g.
configfile: 'config.yaml'

SDIR=workflow.basedir #snakemake dir

manifest_df = pd.read_csv(config['master_table'], sep='\t', header=0, dtype=str)
manifest_df = manifest_df.set_index(['sample', 'hap'], drop=False)
manifest_df.sort_index(inplace=True)

########## wildcard
wildcard_constraints:
    samp='|'.join(manifest_df['sample'].unique()),
    hap='|'.join(manifest_df['hap'].unique()),
    gene_oi = '|'.join( list(config['gene_oi'].keys()) ),
    rgn = '|'.join( list(config['regions'].keys()) )
##########

########## ALL HELPER FUNCTIONS
def get_geneOI_path(wc):
    '''return path from config of gene_OI.'''
    return config['gene_oi'][wc.gene_oi]

def get_query_fa(wc):
    '''get query fasta file from manifest given wildcards. Return file path'''
    return manifest_df.at[(wc.samp, wc.hap), 'fasta']

def get_ref(wc):
    '''get reference fasta from config. return file path'''
    return config['ref']

def get_rgn_bed(wc):
    '''get region bed given wildcard region'''
    return config['regions'][wc.rgn]

def get_all_loc(wc):
    '''return names for all loc_fa for all regions, samples, haps in manifest. return list of fa files for rule get_all_loc_seq'''
    out_list = []
    for r in config['regions'].keys():
        out_list = out_list + [f"results/rgn_seq/{r}/{r}_rgn_{s}_{h}.fa" for s,h in manifest_df.index]
    return out_list
###############


# rule all:
#     input:
#     expand()

rule map_geneOI:
    '''map gene of interest to query haplotype with minimap2 and return paf file of all coordinates
       input: query hap, gene_oi
       output: 1. paf of alignments ; 2. bed of geneOIs in query
    '''
    input:
        gene_OI_fa = get_geneOI_path,
        query = get_query_fa
    output:
        bed = "results/geneOI_mapping/{gene_oi}_{samp}_{hap}.bed",
    resources:
        mem_mb=300
    threads: 4
    conda: "envs/env.yml" 
    shell:'''
    minimap2 -ax asm20 --secondary=yes -p 0.3 -N 10000 --eqx -t {threads} -r 500 -K 200M {input.query} {input.gene_OI_fa} | \
	samtools view -b - | \
	samtools sort | \
	bedtools bamtobed -i - | \
    awk -F "\t" 'BEGIN{{OFS=FS}}{{$4="{wildcards.samp}_{wildcards.hap}-{wildcards.gene_oi}-"NR; print $0}}' > {output.bed}
'''

rule get_unique:
    '''
    get unique sequence of referecne regions of interest to be mapped to queries to pull regions
    @input: region bam, reference fasta
    @output: fasta of region sequence
    '''
    input:
        ref = get_ref,
        rgn_bed=get_rgn_bed
    output:
        fa = temp("tmp/{rgn}.fa")
    threads: 2
    resources:
        mem_mb = 200
    conda: "envs/env.yml"
    shell: '''
    bedtools getfasta -s -fi {input.ref} -bed {input.rgn_bed} -fo {output.fa} -nameOnly
'''

rule map_unique:
    '''map unique sequence of region of interest to query and return paf of unique alignment coordinates
       input: 1. query hap (fasta), 2. unique sequence of loc (fasta)
       output: paf file of unique sequence mappings
    '''
    input:
        query = get_query_fa ,
        rgn_fa=rules.get_unique.output.fa
    output:
        paf = temp("tmp/{rgn}_{samp}_{hap}_mapping.paf")
    threads: 8
    resources:
        mem_mb = 2000
    conda: "envs/env.yml"
    shell:'''
    minimap2 -x asm10 --secondary=no --eqx -t {threads} -r 500 -K 100M {input.query} {input.rgn_fa} > {output.paf}
'''

rule get_loc_bed:
    '''process paf from map_unique and return bedfile of region of interest
       input: paf file from map_unique
       output: bed file of merged location
    '''
    input:
        paf = rules.map_unique.output.paf
    output:
        bed = "tmp/{rgn}_{samp}_{hap}_rgn.bed"
    threads: 2
    resources:
        mem_mb = 200
    conda: "envs/env.yml"
    shell:'''
python {SDIR}/scripts/get_loc_bed.py --paf {input.paf} --bed {output.bed} --rgn {wildcards.rgn} --sample {wildcards.samp} --hap {wildcards.hap}
'''

rule get_loc_gene_bed:
    '''process paf from rule.map_geneOI and rule.get_loc_bed to get location specific bed.
       input: 1. gene bed from rule map_geneOI; 2. bed from rule get_loc_bed
       output: 1. gene bed of genes of interest in locus region
    '''
    input:
        loc_bed = rules.get_loc_bed.output.bed,
        gene_bed = rules.map_geneOI.output.bed
    output:
        bed = "tmp/{rgn}_rgn_{gene_oi}_{samp}_{hap}.bed"
    threads: 2
    resources:
        mem_mb = 200
    conda: "envs/env.yml"
    shell:'''
bedtools intersect -a {input.gene_bed} -b {input.loc_bed} -wa > {output.bed}
'''

# rule get_pass_fail_haps:
#     '''figure out samples in master_table that passed from this approach
#        input: master table, all get_loc_bed
#        output: tab_separated file of each sample if passed or failed
#     '''

rule get_loc_seq:
    '''get location specific sequence
       input: 1. bed from get_loc_bed; 2. query hap (fasta)
       output: fasta sequence
    '''
    input:
        loc_bed = rules.get_loc_bed.output.bed,
        query = get_query_fa
    output:
        fa = "results/rgn_seq/{rgn}/{rgn}_rgn_{samp}_{hap}.fa"
    threads: 2
    resources:
        mem_mb = 200
    conda: "envs/env.yml"
    log: "logs/q_h_seq_results.log"
    shell:'''
# Check if the word "Fail" exists in the file
if grep -q "Fail" {input.loc_bed}; then
    cat {input.loc_bed} > {log}
    touch {output.fa}
else
    bedtools getfasta -fi {input.query} -bed {input.loc_bed} -s -nameOnly -fo {output.fa}
fi
'''

rule get_gene_seq:
    '''get gene bed specific sequence
       input: 1. bed from rule map_geneOI; 2. query hap (fasta)
       output: fasta sequence
    '''
    input:
        gene_bed = rules.map_geneOI.output.bed,
        query = get_query_fa
    output:
        fa = "results/geneOI_mapping/sequence/{gene_oi}_{samp}_{hap}_mappings.fa"
    threads: 2
    resources:
        mem_mb = 200
    conda: "envs/env.yml"
    shell:'''
if grep -q "Fail" {input.loc_bed}; then
    touch {output.fa}
else
    bedtools getfasta -fi {input.query} -bed {input.gene_bed} -s -nameOnly -fo {output.fa}
fi
'''

rule get_loc_gene_seq:
    '''get gene bed specific sequence
       input: 1. bed from rule get_loc_gene_bed; 2. query hap (fasta)
       output: fasta sequence
    '''
    input:
        gene_bed = rules.get_loc_gene_bed.output.bed,
        query = get_query_fa
    output:
        fa = "results/geneOI_mapping/sequence/{rgn}/{rgn}_{gene_oi}_{samp}_{hap}_mappings.fa"
    threads: 2
    resources:
        mem_mb = 200
    conda: "envs/env.yml"
    shell:'''
if grep -q "Fail" {input.loc_bed}; then
    touch {output.fa}
else
    bedtools getfasta -fi {input.query} -bed {input.gene_bed} -s -nameOnly -fo {output.fa}
fi
'''

rule get_all_loc_seq:
    '''get all loc seq given master_tbl and region of interest. Return all fasta files'''
    input:
        all_loc = get_all_loc
